apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.dab.zx"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.name == 'release') {
                variant.mergedFlavor.versionCode = getVersionCode(false)
                // release
                outputFileName = "Zillions-of-enemy-X_v${variant.versionCode}.apk"
            } else {
                variant.mergedFlavor.versionCode = getVersionCode(true)
                // debug
                outputFileName = "Zillions-of-enemy-X_v${variant.versionCode}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    // Support
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.1.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    compile 'com.android.support:cardview-v7:26.1.0'
    // Google
    compile 'com.github.bumptech.glide:glide:3.7.0'
    // ButterKnife
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    // Rx
    compile 'io.reactivex.rxjava2:rxjava:2.0.8'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.github.MFlisar:RxBus2:0.1'
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    // Retrofit
    compile 'com.squareup.retrofit2:converter-gson:2.0.1'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    // FileDownloader
    compile 'com.liulishuo.filedownloader:library:1.4.1'
    // OKHttp
    compile 'com.squareup.okhttp3:okhttp:3.7.0'
    // Linq
    compile 'br.com.zbra:android-linq:1.0.1'
    // SwipeBackLayout
    compile 'me.imid.swipebacklayout.lib:library:1.1.0'
    // Banner
    compile 'com.youth.banner:banner:1.4.5'
}

// 获取 version code
static def getVersionCode(boolean isDebug) {
    if (isDebug) {
        return Integer.parseInt(new Date().format("yyMMddHHmm"))
    }
    return getRevisionNumber()
}

// 获取修订版本 git 提交次数
static def getRevisionNumber() {
    Process process = "git rev-list --count HEAD".execute()
    process.waitFor()
    return process.getText().toInteger()
}
